apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply from: "$rootDir/maven-upload.gradle"

android {
    compileSdkVersion rootProject.ext.compile_sdk_version
    buildToolsVersion rootProject.ext.build_tools_version

    defaultConfig {
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version
        versionCode rootProject.ext.project_version_code
        versionName rootProject.ext.project_version_name
        consumerProguardFiles "proguard-rules.pro"
    }

    buildTypes {
        debug {
            minifyEnabled rootProject.ext.minify_enabled
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled rootProject.ext.minify_enabled
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    libraryVariants.all { variant ->
        def rootProjectName = rootProject.name
        def fullProjectName = project.name
        def parentProject = project.parent
        while (project.rootProject != parentProject) {
            fullProjectName = "${parentProject.name}-${fullProjectName}"
            parentProject = parentProject.parent
        }
        def versionName = project.version
        def buildType = variant.buildType.name
        variant.outputs.all { output ->
            def fileName = "${rootProjectName}-${fullProjectName}-${versionName}-${buildType}.aar"
            output.outputFileName = fileName
        }
    }
}

dependencies {
    implementation rootProject.ext.dependencies.kotlin_stdlib
    implementation rootProject.ext.dependencies.kotlin_stdlib_jdk7
}